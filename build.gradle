apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.2.41',
    minSdk: 9,
    compileSdk: 27,
    lint: '26.2.0-beta01',
  ]

  ext.deps = [
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
    ],
    'lint': [
      'core': "com.android.tools.lint:lint:${versions.lint}",
      'api': "com.android.tools.lint:lint-api:${versions.lint}",
      'tests': "com.android.tools.lint:lint-tests:${versions.lint}",
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.11.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.10.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.2.0'

    classpath 'com.android.tools.build:gradle:3.1.0'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

codeQualityTools {
  ktlint {
    toolVersion = '0.23.1'
  }
  detekt {
    toolVersion = '1.0.0.RC7'
  }
  findbugs {
    enabled = false // https://github.com/vanniktech/gradle-code-quality-tools-plugin/pull/126
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

wrapper {
  gradleVersion = '4.8'
  distributionType = Wrapper.DistributionType.ALL
}
