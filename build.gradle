apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'
apply plugin: 'com.vanniktech.android.junit.jacoco'

buildscript {
  ext.versions = [
    kotlin: '1.3.21',
    minSdk: 9,
    compileSdk: 28,
    lint: '26.5.0-alpha09',
  ]

  ext.deps = [
    'kotlin': [
      'stdlib': "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
    ],
    'lint': [
      'core': "com.android.tools.lint:lint:${versions.lint}",
      'api': "com.android.tools.lint:lint-api:${versions.lint}",
      'tests': "com.android.tools.lint:lint-tests:${versions.lint}",
    ],
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.vanniktech:gradle-android-junit-jacoco-plugin:0.12.0'
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.17.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.8.0'

    classpath 'com.android.tools.build:gradle:3.5.0-alpha09'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

codeQualityTools {
  ktlint {
    toolVersion = '0.30.0'
  }
  detekt {
    toolVersion = '1.0.0-RC12'
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
    jcenter()
  }
}

subprojects { project ->
  project.plugins.withId("com.android.library") {
    // TODO replace with https://issuetracker.google.com/issues/72050365 once released.
    project.android.libraryVariants.all {
      it.generateBuildConfigProvider.configure { it.enabled = false }
    }
  }
}

wrapper {
  gradleVersion = '5.3.1'
  distributionType = Wrapper.DistributionType.ALL
}
