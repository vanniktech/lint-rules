apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.vanniktech.code.quality.tools'

buildscript {
  ext.versions = [
    kotlin: '1.4.32',
    minSdk: 9,
    compileSdk: 30,
    lint: '30.0.1',
  ]

  ext.deps = [
    'lint': [
      'core': "com.android.tools.lint:lint:${versions.lint}",
      'api': "com.android.tools.lint:lint-api:${versions.lint}",
      'tests': "com.android.tools.lint:lint-tests:${versions.lint}",
    ],
    junit: 'junit:junit:4.13.2',
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
  dependencies {
    classpath 'com.vanniktech:gradle-code-quality-tools-plugin:0.20.0'
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.17.0'

    classpath 'com.android.tools.build:gradle:7.0.1'
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.39.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }
}

codeQualityTools {
  ktlint {
    toolVersion = '0.41.0'
  }
  detekt {
    enabled = false
  }
  errorProne {
    enabled = false
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

wrapper {
  gradleVersion = '7.1.1'
  distributionType = Wrapper.DistributionType.ALL
}
